apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 7

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "fr.paug.androidmakers"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "YOUTUBE_API_KEY", "\"" + System.getProperty("androidMakersYouTubeApiKey", "dummyAPIKey") + "\""
        multiDexEnabled true
    }

    flavorDimensions 'environment'

    productFlavors {
        prod {
            dimension 'environment'
        }

        preprod {
            dimension 'environment'
            applicationIdSuffix ".preprod"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Rename output apk (Just for manual signing)
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def formattedDate = new Date().format('yyyy-MM-dd_HH-mm-ss')
                    def newName = output.outputFile.name
                    newName = newName.replace("app", "AndroidMakers")
                    newName = newName.replace("-release", "-release-" + versionName + "-" + versionCode + "-" + formattedDate)
                    outputFileName = newName
                }
            }
        }
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/INDEX.LIST'
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    
    // Support
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:support-vector-drawable:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:percent:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:support-emoji:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.4"
    implementation "com.google.firebase:firebase-database:16.0.3"
    implementation "com.google.firebase:firebase-messaging:17.3.3"
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    // Image management
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    // Used for tags
    implementation 'org.apmem.tools:layouts:1.10@aar'

    // For Video preview
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    // Dialogflow SDK dependencies
//    implementation 'ai.api:sdk:2.0.7@aar'
//    implementation "com.android.support:appcompat-v7:27.1.1"
//    implementation 'ai.api:libai:1.6.12'
    implementation 'com.google.cloud:google-cloud-dialogflow:0.67.0-alpha'
    implementation 'io.grpc:grpc-okhttp:1.15.1'
    implementation 'io.grpc:grpc-protobuf-lite:1.15.1'
    implementation 'io.grpc:grpc-stub:1.15.1'
}

apply plugin: 'com.google.gms.google-services'